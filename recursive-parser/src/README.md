# Логические формулы в стиле Си

Логические формулы. Используются операции `&, |, ^, !`. Приоритет
операций стандартный. Скобки могут использоваться для изменения
приоритета.
В качестве операндов выступают переменные с именем из одной буквы.
Используйте один терминал для всех переменных. Для каждой логической
операции должен быть заведен один терминал.

Пример: `(!a | b) & a & (a | !(b ^ c))`

### Построим КС грамматику:
```
S -> S|A
S -> A
A -> A^B
A -> B
B -> B&C
B -> C
C -> !D
C -> D
D -> (S)
D -> var
```

### Уберем левую рекурсию:
```
S -> AS'
S -> A
S' -> |AS'
S' -> |A
A -> BA'
A -> B
A' -> ^BA'
A' -> ^B
B -> CB'
B -> C
B' -> &CB'
B' -> &C
C -> !D
C -> D
D -> (S)
D -> var
```

### Уберем правое ветвление:
```
S -> AT
S' -> |AT
T -> S'
T -> eps
A -> BU
A' -> ^BU
U -> A'
U -> eps
B -> CV
B' -> &CV
V -> B'
V -> eps
C -> !D
C -> D
D -> (S)
D -> var
```

### Раскроем некоторые нетерминалы:
```
S -> AT
T -> |AT
T -> eps
A -> BU
U -> ^BU
U -> eps
B ->  CV
V -> &CV
V -> eps
C -> !D
C -> D
D -> (S)
D -> var
```

### Построим множества FIRST и FOLLOW
```
+-------+-------------+-----------------+
|       |    FIRST    |     FOLLOW      |
+-------+-------------+-----------------+
|   S   |  var, (, !  |  ), $           |
+-------+-------------+-----------------+
|   T   |  eps, |     |  ), $           |
+-------+-------------+-----------------+
|   A   |  var, (, !  |  $, |, )        |
+-------+-------------+-----------------+
|   U   |  eps, ^     |  $, |, )        |
+-------+-------------+-----------------+
|   B   |  var, (, !  |  $, ^, ), |     |
+-------+-----------+-+-----------------+
|   V   |  eps, &     |  $, ^, ), |     |
+-------+-------------+-----------------+
|   C   |  var, (, !  |  $, ^, ), |, &  |
+-------+-------------+-----------------+
|   D   |  var, (     |  $, ^, ), |, &  |
+-------+-------------+-----------------+
```